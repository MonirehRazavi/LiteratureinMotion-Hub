Group 3 - Vernacular Modernities

Subject. 
Global translation Networks of Canadian Literature. 

Topic.
Mapping how Canadian-authored literary works circulate across world languages and publishing geographies through translations.

Goal.
The goal of this project is to visualize and compare where Canadian literature travels by examining:
Which language Canadian works are translated into 
Which countries publish these translations
How translation patterns difference between English-Canadian and French-Canadian authors
Where Canadian literature “moves” geographically through its translated editions. 

SELECT DISTINCT
  ?work ?workLabel
  ?origLang ?origLangLabel
  ?author ?authorLabel
  ?translatedWork ?translatedWorkLabel
  ?transLang ?transLangLabel
  ?pubPlace ?pubPlaceLabel
  ?pubCountry ?pubCountryLabel
WHERE {

  # 1. Canadian authors
  ?work wdt:P50 ?author .
  ?author wdt:P27 wd:Q16 .          # Canada

  # 2. Original language = English or French
  VALUES ?origLang { wd:Q1860 wd:Q150 }   # English, French
  ?work wdt:P407 ?origLang .

  # 3. Translated editions of those works
  ?translatedWork wdt:P629 ?work ;
                  wdt:P407 ?transLang .

  # 4. Make sure it's actually a translation, not just another ENG/FR edition
  FILTER(?transLang != ?origLang)

  # 5. Publication place & country of the translated edition
  OPTIONAL {
    ?translatedWork wdt:P291 ?pubPlace .
    OPTIONAL {
      ?pubPlace wdt:P17 ?pubCountry .
    }
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
